plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.icezhg'
version = '1.0.0'
description = 'iris'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    configureEach {
        resolutionStrategy.failOnVersionConflict()
    }
}

repositories {
    mavenLocal()
    maven {
        name = 'release'
        url = 'https://packages.aliyun.com/maven/repository/2027113-release-yjsA6r/'
        credentials(PasswordCredentials)
    }
    maven {
        name = 'snapshot'
        url = 'https://packages.aliyun.com/maven/repository/2027113-snapshot-4NJMQr/'
        credentials(PasswordCredentials)
    }
    maven { url = 'https://repo1.maven.org/maven2/' }
    maven { url = 'https://maven.aliyun.com/repository/public' }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.session:spring-session-data-redis'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.jboss.threads:jboss-threads:3.5.0.Final'
    implementation 'org.wildfly.common:wildfly-common:1.5.4.Final'

    implementation 'commons-codec:commons-codec:1.18.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-pool2:2.12.1'
    implementation 'es.moki.ratelimitj:ratelimitj-inmemory:0.7.0'
    implementation 'eu.bitwalker:UserAgentUtils:1.21'
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.55'

    implementation 'com.icezhg:auth:1.4'
    implementation 'com.icezhg:captcha:1.0.0'
    implementation 'com.icezhg:commons:2.1.0'
    implementation 'com.icezhg:spring-boot-starter-encryptor:1.1.0'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
